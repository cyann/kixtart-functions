; Convert a date (YYYY/MM/DD or YYYY-MM-DD) to the number of days since day 1 or vice-versa.
function serial_date($serial)
	dim $z, $h, $a, $b, $c, $y, $m, $d
	if instr($serial, "/") or instr($serial, "-")
		dim $ymd_date_array[]
		if instr($serial, "/")
			$ymd_date_array = split($serial, "/")
		else
			$ymd_date_array = split($serial, "-")
		endif
		$y = val($ymd_date_array[0])
		$m = val($ymd_date_array[1])
		$d = val($ymd_date_array[2])
		if $m < 3
			$m = $m + 12
			$y = $y - 1
		endif
		$serial_date = $d + (((153 * $m) - 457) / 5) + (365 * $y) + ($y / 4) - ($y / 100) + ($y / 400) - 306
	else
		$z = 0 + $serial + 306
		$h = (100 * $z) - 25
		$a = $h / 3652425
		$b = $a - ($a / 4)
		$y = ((100 * $b) + $h) / 36525
		$c = $b + $z - (365 * $y) - ($y / 4)
		$m = ((5 * $c) + 456) / 153
		$d = $c - ((153 * $m) - 457) / 5
		if $m > 12
			$y = $y + 1
			$m = $m - 12
		endif
		$serial_date = right("0000" + $y, 4) + "-" + right("00" + $m, 2) + "-" + right("00" + $d, 2)
	endif
endfunction
