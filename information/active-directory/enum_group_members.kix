; Return an array with the group's members.
function enum_group_members($computer_or_domain, $group, optional $filter)
	; enum_group_members(<computer|domain>, <group name>, [0|1|2])
	; Filter:
	;	0 = All
	;	1 = Users only
	;	2 = Groups only
	dim $member, $new_member_array
	redim $enum_group_members[0]
	$filter = val($filter)
	if $computer_or_domain = @wksta
		$computer_or_domain = @domain + "/" + @wksta
	endif
	$group = getobject("WinNT://" + $computer_or_domain + "/" + $group)
	if vartype($group) <> 9
		exit(@error)
	endif
	for each $member in $group.members
		if substr($member.name, len($member.name), 1) <> chr(36)
			$new_member_array = split(substr($member.adspath, 9), "/")
			select
				case ubound($new_member_array) = 2 and $filter <> 2
					$enum_group_members[ubound($enum_group_members)] = $new_member_array[1] + "\" + $new_member_array[2]
					redim preserve $enum_group_members[ubound($enum_group_members) + 1]
				case ubound($new_member_array) = 1 and $filter <> 1
					$enum_group_members[ubound($enum_group_members)] = $new_member_array[0] + "\" + $new_member_array[1]
					redim preserve $enum_group_members[ubound($enum_group_members) + 1]
			endselect
		endif
	next
	if ubound($enum_group_members)
		redim preserve $enum_group_members[ubound($enum_group_members) - 1]
	endif
endfunction
